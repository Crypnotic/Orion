// Buildscript configuration
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name = 'gradle-plugins'
            url = 'https://plugins.gradle.org/m2'
        }
    }

    dependencies {
        classpath group: 'gradle.plugin.net.minecrell', name: 'licenser', version: minecrell_licenser_version
        //classpath group: 'org.junit.platform', name: 'junit-platform-gradle-plugin', version: junit_gradle_version
        classpath group: 'com.github.jengelman.gradle.plugins', name: 'shadow', version: shadow_version
    }
}

// Gradle plugins
apply plugin: 'idea'
apply plugin: 'eclipse'

// Subproject configurations
allprojects {
    // Project information
    group = 'eu.mikroskeem'
    version = properties.version
    description = properties.description
    url = properties.url

    // Repositories
    repositories {
        mavenLocal()
        mavenCentral()

        maven {
            name = 'mikroskeem-repo'
            url = 'https://repo.wut.ee/repository/mikroskeem-repo'
        }

        maven {
            name = 'destroystokyo-repo'
            url = 'https://repo.destroystokyo.com/repository/maven-public'
        }

        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
}

subprojects {
    //apply plugin: 'org.junit.platform.gradle.plugin'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.minecrell.licenser'
    apply plugin: 'java'

    // Configurations
    configurations {
        downloadDep

        compile.extendsFrom downloadDep
    }

    // Java compiler options
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    // Licenser task
    license {
        header = rootProject.file('etc/HEADER')
        ignoreFailures = false
        include "**/*.java"
    }

    // Global dependencies
    dependencies {
        //testCompile group:'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
        //testRuntime group:'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version
    }

    // downloadDep configuration collector
    task collectDependencies {
        subprojects {
            inputs.file(configurations.downloadDep)
        }
        outputs.file(new File(temporaryDir, 'deps.txt'))

        doLast {
            def outputFile = new File(temporaryDir, 'deps.txt')
            def deps = [] as Set
            rootProject.subprojects.each { subproject ->
                subproject.configurations.downloadDep.resolvedConfiguration.resolvedArtifacts.each { dep ->
                    deps += dep.id.componentIdentifier.toString()
                }
            }
            outputFile.createNewFile()
            outputFile.withWriter { BufferedWriter writer ->
                deps.each {
                    writer.writeLine it
                }
            }
        }
    }
}

// Wrapper task
task wrapper(type: Wrapper) {
    gradleVersion = gradle_version
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

defaultTasks 'build'